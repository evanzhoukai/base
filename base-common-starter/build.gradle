buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
    }
}

plugins {
    id 'se.patrikerdes.use-latest-versions' version '0.2.15'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'com.github.spotbugs' version '4.5.0'
    id 'com.diffplug.spotless' version '5.6.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

apply plugin: 'project-report'
apply from: "${rootProject.projectDir}/gradle/latest-version.gradle"

wrapper {
    gradleVersion = '5.6.4'
}

subprojects {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: "idea"
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: "io.spring.dependency-management"

    group = 'com.github.liaomengge'
    version = '2.0.1-SNAPSHOT'

    apply from: "${rootProject.projectDir}/gradle/publish-properties.gradle"
    apply from: "${rootProject.projectDir}/gradle/publish-jar.gradle"

    apply from: "${rootProject.projectDir}/gradle/spotless.gradle"
    apply from: "${rootProject.projectDir}/gradle/spotbug.gradle"

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    tasks.withType(Javadoc).all {
        enabled = false
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    [compileJava, compileTestJava].each() {
        //it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
        it.options.compilerArgs += ["-Xlint:deprecation"]
        it.options.encoding = "UTF-8"
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://127.0.0.1:8089/repository/fly-snapshots/'
        }
        maven {
            url 'http://127.0.0.1:8089/repository/fly-releases/'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
        jcenter()
    }

    ext {
        baseSpringCloudVersion = '2.0.1-SNAPSHOT'
        baseCommonMiddlewareVersion = '1.1.5-SNAPSHOT'
        baseDayuVersion = '1.1.5-SNAPSHOT'
        baseCacheVersion = '1.1.5-SNAPSHOT'
        baseCommonUtilsVersion = '1.1.5-SNAPSHOT'
    }

    def versions = [
            platformBom: '1.0.3-SNAPSHOT'
    ]

    dependencyManagement {
        dependencies {
            imports {
                mavenBom "com.github.liaomengge:base-platform-bom:${versions.platformBom}"
            }
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    dependencies {
        api "com.github.liaomengge:base-common-utils:${baseCommonUtilsVersion}"

        api "org.springframework.boot:spring-boot-starter"
        api "org.springframework.boot:spring-boot-starter-actuator"
        api "org.springframework.cloud:spring-cloud-context"

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation "junit:junit"
    }
    compileJava.dependsOn(processResources)
}