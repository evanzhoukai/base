plugins {
    id "io.spring.dependency-management" version '1.0.9.RELEASE'
}

wrapper {
     gradleVersion = '6.5'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: "idea"
    apply plugin: 'project-report'
    apply plugin: "io.spring.dependency-management"

    group = 'cn.ly'
    version = '1.0.7-SNAPSHOT'

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    [compileJava, compileTestJava].each() {
        //it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
        it.options.compilerArgs += ["-Xlint:deprecation"]
        it.options.encoding = "UTF-8"
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://127.0.0.1:8089/repository/fly-snapshots/'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
        jcenter()
    }

    jar {
        manifest {
            attributes(
                    "Created-By": "liaomengge",
                    "Implementation-Version": project.version
            )
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
            }
            resources {
                srcDirs = ['src/main/resources']
            }
        }
        test {
            java {
                srcDirs = ['src/test/java']
            }
            resources {
                srcDirs = ['src/test/resources']
            }
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
    }

    ext {
        baseCommonMiddlewareVersion = '1.1.0-SNAPSHOT'
        baseCommonUtilsVersion = '1.0.9-SNAPSHOT'
        nexusUrl = version.endsWith("-SNAPSHOT") ?
                "http://127.0.0.1:8089/repository/fly-snapshots/" :
                "http://127.0.0.1:8089/repository/fly-releases/"
    }

    def versions = [
            platformBom: '0.0.5-SNAPSHOT'
    ]

    dependencyManagement {
        dependencies {
            imports {
                mavenBom "cn.ly:base-platform-bom:${versions.platformBom}"
            }
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        api "com.javadeep:functional-lang"
        api("cn.ly:base-common-middleware:${baseCommonMiddlewareVersion}") {
            exclude(module: 'base-common-utils')
        }

        api "cn.ly:base-common-utils:${baseCommonUtilsVersion}"

        /********************************华丽的分割线****************************/

        api("com.baidu.disconf:disconf-client") {
            exclude(module: 'log4j')
            exclude(module: 'logback-core')
            exclude(module: 'logback-classic')
            exclude(module: 'slf4j-log4j12')
            exclude(group: 'org.springframework')
        }

        api("com.alibaba:dubbo") {
            exclude(module: 'log4j')
            exclude(module: 'logback-classic')
            exclude(module: 'slf4j-log4j12')
            exclude(group: 'org.springframework')
        }

        api "org.glassfish.web:javax.el"
        api "org.apache.tomcat.embed:tomcat-embed-core"
        api "org.apache.tomcat.embed:tomcat-embed-logging-juli"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation "junit:junit"
    }
}



