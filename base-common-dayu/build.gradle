plugins {
    id 'se.patrikerdes.use-latest-versions' version '0.2.14'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

group = 'cn.ly'
version = '1.1.2-SNAPSHOT'

targetCompatibility = 1.8
sourceCompatibility = 1.8

[compileJava, compileTestJava].each() {
    //it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
    it.options.compilerArgs += ["-Xlint:deprecation"]
    it.options.encoding = "UTF-8"
}

dependencyReport {
    projects = project.allprojects
    outputFile = file("$buildDir/reports/dependencies.txt")
}

jar {
    manifest {
        attributes(
                "Created-By": "liaomengge",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

ext {
    nexusUrl = version.endsWith("-SNAPSHOT") ?
            "http://127.0.0.1:8089/repository/fly-snapshots/" :
            "http://127.0.0.1:8089/repository/fly-releases/"
}

dependencies {
    api project(":base-common-utils")

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation "junit:junit"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: nexusUrl) {
                authentication(userName: "fly", password: "fly123")
            }
            pom.version = "$project.version"
            pom.groupId = "$project.group"
        }
    }
}
